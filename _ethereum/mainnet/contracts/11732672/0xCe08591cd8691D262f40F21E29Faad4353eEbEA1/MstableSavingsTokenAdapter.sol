// Copyright (C) 2020 Zerion Inc. <https://zerion.io>
//
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with this program. If not, see <https://www.gnu.org/licenses/>.

pragma solidity 0.6.5;
pragma experimental ABIEncoderV2;

import "./ERC20.sol";
import "./Structs.sol";
import "./TokenAdapter.sol";

/**
 * @dev SavingsContract contract interface.
 * Only the functions required for MstableAssetAdapter contract are added.
 * The SavingsContract contract is available here
 * github.com/mstable/mStable-contracts/blob/master/contracts/savings/SavingsContract.sol.
 */
interface SavingsContract {
    function underlying() external view returns (address);
    function exchangeRate() external view returns (uint256);
}


/**
 * @title Token adapter for mStable protocol (Intereast Bearing mUSD).
 * @dev Implementation of TokenAdapter interface.
 */
contract MstableSavingsTokenAdapter is TokenAdapter {

    /**
     * @return TokenMetadata struct with ERC20-style token info.
     * @dev Implementation of TokenAdapter interface function.
     */
    function getMetadata(address token) external view override returns (TokenMetadata memory) {
        return TokenMetadata({
            token: token,
            name: ERC20(token).name(),
            symbol: ERC20(token).symbol(),
            decimals: ERC20(token).decimals()
        });
    }

    /**
     * @return Array of Component structs with underlying tokens rates for the given token.
     * @dev Implementation of TokenAdapter interface function.
     */
    function getComponents(address token) external view override returns (Component[] memory) {
        Component[] memory underlyingTokens = new Component[](1);

        underlyingTokens[0] = Component({
            token: SavingsContract(token).underlying(),
            tokenType: "ERC20",
            rate: SavingsContract(token).exchangeRate()
        });

        return underlyingTokens;
    }
}
