//SPDX-License-Identifier: MIT
/*@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                             @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                         @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                             @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                      @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                         @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                           @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                      @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                     @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@      @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@        @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                   @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@          @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@           @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                 @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@             @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@              @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@              @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@             @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                 @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@           @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@         @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                   @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@        @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@      @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                     @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@   @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                      @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                           @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                         @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                      @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                             @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                        @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                   @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                             @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@*/
/**
 * @dev: @brougkr
 */
pragma solidity 0.8.12;
import "./Ownable.sol";
import "./ReentrancyGuard.sol";
import "./MerkleProof.sol";
import "./IERC721.sol";
import "./IERC1155.sol";
import "./IERC20.sol";

contract Marketplace is Ownable, ReentrancyGuard
{
    /***************************/
    /********** STRUCT *********/
    /***************************/

    struct Sale
    {
        string Name;
        uint PriceBrightList;
        uint PricePublic;
        uint StartingIndex;
        uint EndingIndex;
        uint PurchaseableAmountBrightList;
        uint PurchaseableAmountPublic;
        uint ERC_TYPE;
        address ContractAddress;
        address Operator;
        bytes32 Root;
        bool ActivePublic;
        bool ActiveBrightList;
        bool AllowMultiplePurchases;
    }

    /***************************/
    /********* MAPPINGS ********/
    /***************************/

    mapping(uint => Sale) private Sales;
    mapping(uint => uint) public SaleProceeds;
    mapping(address => bytes32) public BRTOperators;
    mapping(uint => mapping(address => bool)) public MarketplacePurchased;
    mapping(uint => mapping(address => uint)) public PurchasedAmountBrightList;
    mapping(uint => mapping(address => uint)) public PurchasedAmountPublic;

    /***************************/
    /********** EVENTS *********/
    /***************************/

    /**
     * @dev Emitted When A BrightList Purchase Occurs
     */
    event MarketplacePurchaseEventBrightList(
        address indexed recipientAddress, 
        uint indexed SaleIndex, 
        uint amount
    );

    /**
     * @dev Emitted When A Public Purchase Occurs
     */
    event MarketplacePurchaseEvent(
        address indexed recipientAddress, 
        uint indexed SaleIndex, 
        uint amount
    );

    /**
     * @dev Emitted When A Sale Has Started
     */
    event SaleStarted(
        string Name,
        address ContractAddress, 
        bytes32 RootHash, 
        address Operator, 
        bool ActivePublic,
        bool ActiveBrightList,
        bool AllowMultiplePurchases
    );

    /**
     * @dev Emitted When A Batch Of Sales Have Been Initialized
     */
    event SalesStarted(uint StartingSaleIndex, uint EndingSaleIndex);

    /**
     * @dev Emitted When Various State Variables Are Modified
     */
    event SaleEnded(uint SaleIndex);
    event SaleChangedName(uint indexed SaleIndex, string OldName, string NewName);
    event SaleChangedStartingIndex(uint indexed SaleIndex, uint OldStartingIndex, uint NewStartingIndex);
    event SaleChangedEndingIndex(uint indexed SaleIndex, uint OldEndingIndex, uint NewEndingIndex);
    event SaleChangedAllocationPublic(uint indexed SaleIndex, uint OldAllocation, uint NewAllocation);
    event SaleChangedAllocationBrightList(uint indexed SaleIndex, uint OldAllocation, uint NewAllocation);
    event SaleChangedPriceBrightList(uint indexed SaleIndex, uint OldPrice, uint NewPrice);
    event SaleChangedPricePublic(uint indexed SaleIndex, uint OldPrice, uint NewPrice);
    event SaleChangedERC_TYPE(uint indexed SaleIndex, uint OLD_ERC_TYPE, uint NEW_ERC_TYPE);
    event SaleChangedContract(uint indexed SaleIndex, address OldContract, address NewContract);
    event SaleChangedRoot(uint indexed SaleIndex, bytes32 OldRoot, bytes32 NewRoot);
    event SaleChangedOperator(uint indexed SaleIndex, address OldOperator, address NewOperator);
    event SaleChangedActiveState(uint indexed SaleIndex, bool OldState, bool NewState);
    event SaleChangedActiveStateBrightList(uint indexed SaleIndex, bool OldState, bool NewState);
    event SaleChangedActiveStatesPublic(uint[] SaleIndexes, bool[] States);
    event SaleChangedActiveStatesBrightList(uint[] SaleIndexes, bool[] States);

    /**
     * @dev Emitted When BRT Multisig Adds Or Removes An Operator
     */
    event OperatorAdded(address Operator);
    event OperatorRemoved(address Operator);

    /**
     * @dev Amount Of Unique Marketplace Sales
     */
    uint public UniqueSaleIndex;

    /**
     * @dev BRT Operator Roles
     */
    bytes32 private immutable _OPERATOR = keccak256("OPERATOR");
    bytes32 private immutable _DEACTIVATED = 0x0;

    /**
     * @dev Constructor
     */
    constructor() 
    { 
        BRTOperators[0x5778B0B140Fa7a62B96c193cC8621e6E96c088A5] = _OPERATOR; //brougkr
        BRTOperators[0x18782E9d3fB2E8D17CBF7a1F72c5f4e09E263734] = _OPERATOR; //jesse.brightmoments.eth
        BRTOperators[0x18B7511938FBe2EE08ADf3d4A24edB00A5C9B783] = _OPERATOR; //phil.brightmoments.eth
        BRTOperators[0xbf001FF749b7E793bbb1A612d09124470b9179A7] = _OPERATOR; //future
    } 

    /***************************/
    /***** PUBLIC FUNCTIONS ****/
    /***************************/

    /**
     * @dev Marketplace Purchase Public Sale
     */
    function MarketplacePurchase(uint SaleIndex, uint Amount) public payable nonReentrant
    { 
        require(Sales[SaleIndex].ActivePublic, "Requested Sale Is Not Available For Public Purchases");
        require(Sales[SaleIndex].StartingIndex <= Sales[SaleIndex].EndingIndex, "Sold Out");
        require(
            PurchasedAmountPublic[SaleIndex][msg.sender] + Amount <= Sales[SaleIndex].PurchaseableAmountPublic,
            "User Has Used Up All Of Public Allocation For This Sale Index"
        );
        if(!Sales[SaleIndex].AllowMultiplePurchases) { require(!MarketplacePurchased[SaleIndex][msg.sender], "User Has Already Purchased This Sale Index"); }
        require(msg.value == Sales[SaleIndex].PricePublic * Amount && Amount > 0, "Incorrect Ether Amount Or Token Amount Sent For Purchase");
        if(!MarketplacePurchased[SaleIndex][msg.sender]) { MarketplacePurchased[SaleIndex][msg.sender] = true; }
        PurchasedAmountPublic[SaleIndex][msg.sender] += Amount;
        for(uint i; i < Amount; i++)
        {
            if(Sales[SaleIndex].ERC_TYPE == 721)
            {
                IERC721(Sales[SaleIndex].ContractAddress).transferFrom(
                    Sales[SaleIndex].Operator, 
                    msg.sender, 
                    Sales[SaleIndex].StartingIndex
                );
            }
            else if(Sales[SaleIndex].ERC_TYPE == 1155)
            {
                IERC1155(Sales[SaleIndex].ContractAddress).safeTransferFrom(
                    Sales[SaleIndex].Operator, 
                    msg.sender, 
                    Sales[SaleIndex].StartingIndex, 
                    1, 
                    "BRT"
                );
            }
            Sales[SaleIndex].StartingIndex++;
        }
        SaleProceeds[SaleIndex] += msg.value;
        emit MarketplacePurchaseEvent(msg.sender, SaleIndex, Amount);
    }

    /**
     * @dev Marketplace Purchase BrightList Sale
     */
    function MarketplacePurchaseBrightList(uint SaleIndex, uint Amount, bytes32[] calldata Proof) public payable nonReentrant
    {
        require(Sales[SaleIndex].ActiveBrightList, "Requested Sale Is Not Available For BrightList Purchases");
        require(Sales[SaleIndex].StartingIndex <= Sales[SaleIndex].EndingIndex, "Sold Out");
        require(msg.value == Sales[SaleIndex].PriceBrightList * Amount && Amount > 0, "Incorrect Ether Amount Or Token Amount Sent For Purchase");
        require(viewBrightListAllocation(msg.sender, SaleIndex, Proof), "User Is Not On BrightList");
        require(
            PurchasedAmountBrightList[SaleIndex][msg.sender] + Amount <= Sales[SaleIndex].PurchaseableAmountBrightList,
            "User Has Used Up All BrightList Allocation For This Sale Index"
        );
        if(!Sales[SaleIndex].AllowMultiplePurchases) { require(!MarketplacePurchased[SaleIndex][msg.sender], "User Has Already Purchased This Sale Index"); }
        if(!MarketplacePurchased[SaleIndex][msg.sender]) { MarketplacePurchased[SaleIndex][msg.sender] = true; }
        PurchasedAmountBrightList[SaleIndex][msg.sender] += Amount;    
        for(uint i; i < Amount; i++)
        {
            if(Sales[SaleIndex].ERC_TYPE == 721)
            {
                IERC721(Sales[SaleIndex].ContractAddress).transferFrom(
                    Sales[SaleIndex].Operator,
                    msg.sender, 
                    Sales[SaleIndex].StartingIndex
                );
            }
            else if(Sales[SaleIndex].ERC_TYPE == 1155)
            {
                IERC1155(Sales[SaleIndex].ContractAddress).safeTransferFrom(
                    Sales[SaleIndex].Operator,
                    msg.sender, 
                    Sales[SaleIndex].StartingIndex, 
                    1, 
                    "BRT"
                );
            }
            Sales[SaleIndex].StartingIndex++;
        }
        SaleProceeds[SaleIndex] += msg.value;
        emit MarketplacePurchaseEventBrightList(msg.sender, SaleIndex, Amount);
    }

    /***************************/
    /**** _OPERATOR COMMANDS ****/
    /***************************/

    /**
     * @dev Sets Up A New Sale
     * note: `Price` Is Input In WEI Due To Ethereum EVM. For Example, 1 ETH = 1000000000000000000 WEI
     * note: `ERC_TYPE` Is (`721` for ERC721) || (`1155` for ERC1155)
     * note: `ContractAddress` Is The NFT Contract Address
     * note: `RootHash` Is Merkle Root Hash
     * note: `Operator` Is The Wallet Providing The NFTs For Sale. They Will Have To setApprovalForAll() On This Contract
     * note: `Public` & `AllowMultiplePurchases` Are Either `true` or `false`
     */
    function _StartSale(Sale memory NewSaleInstance) external onlyBRTOperator
    {
        //Checks Passed Variables
        require(
            NewSaleInstance.ERC_TYPE == 721 || NewSaleInstance.ERC_TYPE == 1155,
            "Incorrect ERC Type. (721 for ERC721) or (1155 for ERC1155)"
        );

        //Auto-Increments The Unique Sale Index
        UniqueSaleIndex++; 

        //Assigns State Variables To The New Sale Instance
        Sales[UniqueSaleIndex].Name = NewSaleInstance.Name;
        Sales[UniqueSaleIndex].PriceBrightList = NewSaleInstance.PriceBrightList;
        Sales[UniqueSaleIndex].PricePublic = NewSaleInstance.PricePublic;
        Sales[UniqueSaleIndex].StartingIndex = NewSaleInstance.StartingIndex;
        Sales[UniqueSaleIndex].EndingIndex = NewSaleInstance.EndingIndex;
        Sales[UniqueSaleIndex].PurchaseableAmountBrightList = NewSaleInstance.PurchaseableAmountBrightList;
        Sales[UniqueSaleIndex].PurchaseableAmountPublic = NewSaleInstance.PurchaseableAmountPublic;
        Sales[UniqueSaleIndex].ERC_TYPE = NewSaleInstance.ERC_TYPE;
        Sales[UniqueSaleIndex].ContractAddress = NewSaleInstance.ContractAddress;
        Sales[UniqueSaleIndex].Operator = NewSaleInstance.Operator;
        Sales[UniqueSaleIndex].Root = NewSaleInstance.Root;
        Sales[UniqueSaleIndex].ActivePublic = NewSaleInstance.ActivePublic;
        Sales[UniqueSaleIndex].ActiveBrightList = NewSaleInstance.ActiveBrightList;
        Sales[UniqueSaleIndex].AllowMultiplePurchases = NewSaleInstance.AllowMultiplePurchases;
        
        //Emits Base Data Of The New Sale Instance
        emit SaleStarted(
            Sales[UniqueSaleIndex].Name,
            Sales[UniqueSaleIndex].ContractAddress, 
            Sales[UniqueSaleIndex].Root, 
            Sales[UniqueSaleIndex].Operator, 
            Sales[UniqueSaleIndex].ActivePublic,
            Sales[UniqueSaleIndex].ActiveBrightList,
            Sales[UniqueSaleIndex].AllowMultiplePurchases
        );
    }

    /**
     * @dev Batch Startes Multiple Sales
     */
    function _StartSales(Sale[] memory NewSaleInstance) external onlyBRTOperator   
    {
        uint StartingSaleIndex = UniqueSaleIndex;
        for(uint i; i < NewSaleInstance.length; i++)
        {
            //Checks Passed Variables
            require(
                NewSaleInstance[i].ERC_TYPE == 721 || NewSaleInstance[i].ERC_TYPE == 1155,
                "Please Enter Valid ERC-TYPE, (721 for ERC-721), (1155 for ERC-1155)"
            );        

            //Auto Increments Index    
            UniqueSaleIndex++; 

            //Assigns State Variables To The New Sale Instance
            Sales[UniqueSaleIndex].Name = NewSaleInstance[i].Name;
            Sales[UniqueSaleIndex].PriceBrightList = NewSaleInstance[i].PriceBrightList;
            Sales[UniqueSaleIndex].PricePublic = NewSaleInstance[i].PricePublic;
            Sales[UniqueSaleIndex].StartingIndex = NewSaleInstance[i].StartingIndex;
            Sales[UniqueSaleIndex].EndingIndex = NewSaleInstance[i].EndingIndex;
            Sales[UniqueSaleIndex].PurchaseableAmountBrightList = NewSaleInstance[i].PurchaseableAmountBrightList;
            Sales[UniqueSaleIndex].PurchaseableAmountPublic = NewSaleInstance[i].PurchaseableAmountPublic;
            Sales[UniqueSaleIndex].ERC_TYPE = NewSaleInstance[i].ERC_TYPE;
            Sales[UniqueSaleIndex].ContractAddress = NewSaleInstance[i].ContractAddress;
            Sales[UniqueSaleIndex].Operator = NewSaleInstance[i].Operator;
            Sales[UniqueSaleIndex].Root = NewSaleInstance[i].Root;
            Sales[UniqueSaleIndex].ActivePublic = NewSaleInstance[i].ActivePublic;
            Sales[UniqueSaleIndex].ActiveBrightList = NewSaleInstance[i].ActiveBrightList;
            Sales[UniqueSaleIndex].AllowMultiplePurchases = NewSaleInstance[i].AllowMultiplePurchases;
        }
        emit SalesStarted(StartingSaleIndex, UniqueSaleIndex);
    }

    /**
     * @dev Changes BrightList Sale Name `Name` At Index `SaleIndex`
     * note: This Is The Name Of The Sale
     */
    function _ChangeSaleName(uint SaleIndex, string memory Name) external onlyBRTOperator 
    { 
        string memory OldName = Sales[SaleIndex].Name;
        Sales[SaleIndex].Name = Name; 
        emit SaleChangedName(SaleIndex, OldName, Name);
    }
    
    /**
     * @dev Changes BrightList StartingIndex `Index` At Index `SaleIndex`
     * note: This Is The Starting Token ID
     */
    function _ChangeSaleStartingIndex(uint SaleIndex, uint Index) external onlyBRTOperator 
    { 
        uint OldStartingIndex = Sales[SaleIndex].StartingIndex;
        Sales[SaleIndex].StartingIndex = Index; 
        emit SaleChangedStartingIndex(SaleIndex, OldStartingIndex, Index);
    }

    /**
     * @dev Changes BrightList EndingIndex `Index` At Index `SaleIndex`
     * note: This Is The Ending Token ID
     */
    function _ChangeSaleEndingIndex(uint SaleIndex, uint Index) external onlyBRTOperator 
    { 
        uint OldEndingIndex = Sales[SaleIndex].EndingIndex;
        Sales[SaleIndex].EndingIndex = Index; 
        emit SaleChangedEndingIndex(SaleIndex, OldEndingIndex, Index);
    }

    /**
     * @dev Changes Brightlist Public Purchasable Amount `Amount` At Index `SaleIndex`
     * note: This Is The Public Allocation
     */
    function _ChangeSaleAllocationPublic(uint SaleIndex, uint Amount) external onlyBRTOperator 
    {
        uint OldAllocation = Sales[SaleIndex].PurchaseableAmountBrightList; 
        Sales[SaleIndex].PurchaseableAmountPublic = Amount; 
        emit SaleChangedAllocationPublic(SaleIndex, OldAllocation, Amount);
    }
    
    /**
     * @dev Changes Brightlist Purchasable Amount `Amount` At Index `SaleIndex`
     * note: This Is The BrightList Allocation
     */
    function _ChangeSaleAllocationBrightList(uint SaleIndex, uint Amount) external onlyBRTOperator 
    {
        uint OldAllocation = Sales[SaleIndex].PurchaseableAmountBrightList; 
        Sales[SaleIndex].PurchaseableAmountBrightList = Amount; 
        emit SaleChangedAllocationBrightList(SaleIndex, OldAllocation, Amount);
    }

    /**
     * @dev Changes Sale Price Public Amount `Price` At Index `SaleIndex`
     * note: This Is Input In WEI Not In Ether. 1 ETH = 1000000000000000000 WEI
     */
    function _ChangeSalePricePublic(uint SaleIndex, uint Price) external onlyBRTOperator 
    { 
        uint OldPrice = Sales[SaleIndex].PriceBrightList; 
        Sales[SaleIndex].PricePublic = Price;
        emit SaleChangedPricePublic(SaleIndex, OldPrice, Price);
    }

    /**
     * @dev Changes Sale Price BrightList Amount `Price` At Index `SaleIndex`
     * note: This Is Input In WEI Not In Ether. 1 ETH = 1000000000000000000 WEI
     */
    function _ChangeSalePriceBrightList(uint SaleIndex, uint Price) external onlyBRTOperator 
    {
        uint OldPrice = Sales[SaleIndex].PriceBrightList; 
        Sales[SaleIndex].PriceBrightList = Price; 
        emit SaleChangedPriceBrightList(SaleIndex, OldPrice, Price);
    }
    
    /**
     * @dev Changes BrightList ERC Type `ERC_TYPE` At Index `SaleIndex`
     * note: Possible Inputs Are:
     * `721` - ERC721
     * `1155` - ERC1155
     */
    function _ChangeSaleERC_TYPE(uint SaleIndex, uint ERC_TYPE) external onlyBRTOperator 
    {
        require(ERC_TYPE == 721 || ERC_TYPE == 1155, "Incorrect ERC_TYPE"); 
        uint Old_ERC_TYPE = Sales[SaleIndex].ERC_TYPE;
        Sales[SaleIndex].ERC_TYPE = ERC_TYPE; 
        emit SaleChangedERC_TYPE(SaleIndex, Old_ERC_TYPE, ERC_TYPE);
    }

    /**
     * @dev Changes BrightList Contract Address `Contract` At Index `SaleIndex`
     * note: This Is The NFT Address That Is Being Claimed
     */
    function _ChangeSaleContract(uint SaleIndex, address Contract) external onlyBRTOperator 
    { 
        address OldContract = Sales[SaleIndex].ContractAddress;
        Sales[SaleIndex].ContractAddress = Contract; 
        emit SaleChangedContract(SaleIndex, OldContract, Contract);
    }

    /**
     * @dev Changes BrightList Operator `operator` At Index `SaleIndex`
     * note: This Is The Wallet / Address / EOA That The NFTs Are Pulling From
     */
    function _ChangeSaleOperator(uint SaleIndex, address Operator) external onlyBRTOperator 
    { 
        address OldOperator = Sales[SaleIndex].Operator;
        Sales[SaleIndex].Operator = Operator; 
        emit SaleChangedOperator(SaleIndex, OldOperator, Operator);
    }

    /**
     * @dev Changes BrightList Root `RootHash` At Index `SaleIndex`
     * note: This Is The Merkle Root
     */
    function _ChangeSaleRoot(uint SaleIndex, bytes32 RootHash) external onlyBRTOperator 
    { 
        bytes32 OldRoot = Sales[SaleIndex].Root;
        Sales[SaleIndex].Root = RootHash; 
        emit SaleChangedRoot(SaleIndex, OldRoot, RootHash);
    }

    /**
     * @dev Changes Public Sale State `State` At Index `SaleIndex`
     * note: Possible Inputs are `true` or `false`
     */
    function _ChangeSaleActiveStatePublic(uint SaleIndex, bool State) external onlyBRTOperator 
    { 
        bool OldState = Sales[SaleIndex].ActivePublic;
        Sales[SaleIndex].ActivePublic = State; 
        emit SaleChangedActiveState(SaleIndex, OldState, State);
    }

    /**
     * @dev Changes BrightList Sale State `State` At Index `SaleIndex`
     * note: Possible Inputs are `true` or `false`
     */
    function _ChangeSaleActiveStateBrightList(uint SaleIndex, bool State) external onlyBRTOperator 
    { 
        bool OldState = Sales[SaleIndex].ActiveBrightList;
        Sales[SaleIndex].ActiveBrightList = State; 
        emit SaleChangedActiveStateBrightList(SaleIndex, OldState, State);
    }

    /**
     * @dev Changes BrightList Sale States `States` At Index `SaleIndex`
     * note: Possible Inputs are array of `true` or `false`
     */
    function _ChangeSaleActiveStatesBrightList(uint[] calldata SaleIndexes, bool[] calldata States) external onlyBRTOperator 
    { 
        for(uint i; i < SaleIndexes.length; i++)
        {
            Sales[i].ActiveBrightList = States[i];
        }
        emit SaleChangedActiveStatesBrightList(SaleIndexes, States);
    }

    /**
     * @dev Changes Public Sale State `State` At Index `SaleIndex`
     * note: Possible Inputs are array of `true` or `false`
     */
    function _ChangeSaleActiveStatesPublic(uint[] calldata SaleIndexes, bool[] calldata States) external onlyBRTOperator 
    { 
        for(uint i; i < SaleIndexes.length; i++)
        {
            Sales[i].ActiveBrightList = States[i];
        }
        emit SaleChangedActiveStatesPublic(SaleIndexes, States);
    }

    /**
     * @dev Ends Sale At Index `SaleIndex`
     */
    function _EndSale(uint SaleIndex) external onlyBRTOperator 
    { 
        Sales[SaleIndex].ActivePublic = false; 
        emit SaleEnded(SaleIndex);
    }

    /***************************/
    /****** ADMIN COMMANDS *****/
    /***************************/

    /**
     * @dev Adds Bright Moments Operator
     * note: OnlyOwner
     */
    function __OperatorAdd(address Operator) external onlyOwner 
    { 
        BRTOperators[Operator] = _OPERATOR; 
        emit OperatorAdded(Operator);
    }

    /**
     * @dev Removes Bright Moments Operator
     * note: OnlyOwner
     */
    function __OperatorRemove(address Operator) external onlyOwner 
    { 
        BRTOperators[Operator] = _DEACTIVATED; 
        emit OperatorRemoved(Operator);    
    }

    /**
     * @dev Withdraws All Ether From Contract To Owner
     * note: OnlyOwner
     */
    function __Withdraw() external onlyOwner 
    {
        uint balance = address(this).balance;
        require(balance > 0, "Insufficient Balance"); 
        payable(owner()).transfer(balance); 
    }

    /**
     * @dev Withdraws Ether From Contract To Address
     * note: OnlyOwner
     */
    function __WithdrawToAddress(address payable Recipient) external onlyOwner 
    {
        uint balance = address(this).balance;
        require(balance > 0, "Insufficient Ether To Withdraw");
        (bool Success, ) = Recipient.call{value: balance}("");
        require(Success, "Unable to Withdraw, Recipient May Have Reverted");
    }

    /**
     * @dev Withdraws Ether From Contract To Address With An Amount
     * note: OnlyOwner
     * note: `Amount` is Denoted In WEI ()
     */
    function __WithdrawAmountToAddress(address payable Recipient, uint Amount) external onlyOwner
    {
        require(Amount > 0 && Amount <= address(this).balance, "Invalid Amount");
        (bool Success, ) = Recipient.call{value: Amount}("");
        require(Success, "Unable to Withdraw, Recipient May Have Reverted");
    }

    /**
     * @dev Withdraws Ether From Sale Instance To Owner
     * note: OnlyOwner
     */
    function __WithdrawSaleProceeds(uint SaleIndex) external onlyOwner
    {
        uint Amount = SaleProceeds[SaleIndex];
        require(Amount > 0 && Amount <= address(this).balance, "Insufficient Balance");
        SaleProceeds[SaleIndex] = 0;
        (bool Success, ) = owner().call{value: Amount}("");
        require(Success, "Unable to Withdraw, Recipient May Have Reverted");
    }

    /**
     * @dev Withdraws Ether From Sale Instance To Address
     * note: OnlyOwner
     */
    function __WithdrawSaleProceedsToAddress(address payable Recipient, uint SaleIndex) external onlyOwner
    {
        uint Amount = SaleProceeds[SaleIndex];
        require(Amount > 0 && Amount <= address(this).balance, "Insufficient Balance");
        SaleProceeds[SaleIndex] = 0;
        (bool Success, ) = Recipient.call{value: Amount}("");
        require(Success, "Unable to Withdraw, Recipient May Have Reverted");
    }
    
    /**
     * @dev Withdraws ERC20 From Contract To Address
     * note: OnlyOwner
     */
    function __WithdrawERC20ToAddress(address Recipient, address ContractAddress) external onlyOwner
    {
        IERC20 ERC20 = IERC20(ContractAddress);
        ERC20.transferFrom(address(this), Recipient, ERC20.balanceOf(address(this)));
    }

    /***************************/
    /******* PUBLIC VIEW *******/
    /***************************/
    
    /**
     * @dev Checks BrightList Allocation
     */
    function viewBrightListAllocation(address Recipient, uint SaleIndex, bytes32[] memory Proof) public view returns(bool)
    { 
        bytes32 Leaf = keccak256(abi.encodePacked(Recipient));
        return MerkleProof.verify(Proof, Sales[SaleIndex].Root, Leaf);
    }

    /**
     * @dev Returns State Variables Of `SaleIndex`
     * note: `0. Name`
     * note: `1. Price BrightList`
     * note: `2. Price Public`
     * note: `3. StartingIndex`
     * note: `4. EndingIndex`
     * note: `5. Purchaseable Amount BrightList`
     * note: `6. Purchaseable Amount Public`
     * note: `7. ERC_TYPE (721 for ERC721) || (1155 for ERC1155)`
     * note: `8. ContractAddress` Of NFT
     * note: `9. Operator`
     * note: `10. Merkle Root` 
     * note: `11. Is Sale Public Active`
     * note: `12. Is Sale BrightList Active`
     * note: `13. Allow Multiple Purchases`
     */
    function viewSaleState(uint SaleIndex) public view returns(Sale memory) { return Sales[SaleIndex]; }

    /**
     * @dev Returns Multiple Sale Instances Of `SaleIndex`
     * note: `0. Name`
     * note: `1. Price BrightList`
     * note: `2. Price Public`
     * note: `3. StartingIndex`
     * note: `4. EndingIndex`
     * note: `5. Purchaseable Amount BrightList`
     * note: `6. Purchaseable Amount Public`
     * note: `7. ERC_TYPE (721 for ERC721) || (1155 for ERC1155)`
     * note: `8. ContractAddress` Of NFT
     * note: `9. Operator`
     * note: `10. Merkle Root` 
     * note: `11. Is Sale Public Active`
     * note: `12. Is Sale BrightList Active`
     * note: `13. Allow Multiple Purchases`
     */
    function viewSaleStates(uint[] calldata SaleIndexes) public view returns(Sale[] memory) 
    {
        Sale[] memory SaleStates = new Sale[](SaleIndexes.length);
        for (uint i; i < SaleIndexes.length; i++) 
        {
            Sale storage saleInstance = Sales[SaleIndexes[i]];
            SaleStates[i] = saleInstance;
        }
        return SaleStates;
    }

    /***************************/
    /******** MODIFIER *********/
    /***************************/

    /**
     * @dev Restricts Certain Functions To Bright Moments Operators Only
     */
    modifier onlyBRTOperator
    {
        require(BRTOperators[msg.sender] == _OPERATOR, "User Is Not A Valid BRT Operator");
        _;
    }
}
