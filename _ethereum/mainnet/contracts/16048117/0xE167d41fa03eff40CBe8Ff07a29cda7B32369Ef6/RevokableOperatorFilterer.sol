// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

import "./OperatorFilterer.sol";

/**
 * @title  RevokableOperatorFilterer
 * @notice This contract is meant to allow contracts to permanently opt out of the OperatorFilterRegistry. The Registry
 *         itself has an "unregister" function, but if the contract is ownable, the owner can re-register at any point.
 *         As implemented, this abstract contract allows the contract owner to toggle the
 *         isOperatorFilterRegistryRevoked flag in order to permanently bypass the OperatorFilterRegistry checks.
 */
abstract contract RevokableOperatorFilterer is OperatorFilterer {
    error OnlyOwner();
    error AlreadyRevoked();
    error AlreadyEnabled();

    bool private _isOperatorFilterRegistryRevoked;

    function _checkFilterOperator(address operator) internal view virtual override {
        if (!_isOperatorFilterRegistryRevoked) {
            super._checkFilterOperator(operator);
        }
    }

    /**
     * @notice Disable the isOperatorFilterRegistryRevoked flag. OnlyOwner.
     */
    function revokeOperatorFilterRegistry() external {
        if (msg.sender != owner()) {
            revert OnlyOwner();
        }
        if (_isOperatorFilterRegistryRevoked) {
            revert AlreadyRevoked();
        }
        _isOperatorFilterRegistryRevoked = true;
    }

    /**
     * @notice Enable the isOperatorFilterRegistryRevoked flag. OnlyOwner.
     */
    function enableOperatorFilterRegistry() external {
        if (msg.sender != owner()) {
            revert OnlyOwner();
        }
        if (!_isOperatorFilterRegistryRevoked) {
            revert AlreadyEnabled();
        }
        _isOperatorFilterRegistryRevoked = false;
    }

    function isOperatorFilterRegistryRevoked() public view returns (bool) {
        return _isOperatorFilterRegistryRevoked;
    }

    /**
     * @dev assume the contract has an owner, but leave specific Ownable implementation up to inheriting contract
     */
    function owner() public view virtual returns (address);
}