/*

  Proxy registry; keeps a mapping of AuthenticatedProxy contracts and mapping of contracts authorized to access them.  
  
  Abstracted away from the Exchange (a) to reduce Exchange attack surface and (b) so that the Exchange contract can be upgraded without users needing to transfer assets to new proxies.

*/
// SPDX-License-Identifier: None

pragma solidity 0.8.12;

import "./OwnableUpgradeable.sol";
import "./ReentrancyGuardUpgradeable.sol";
import "./OwnableDelegateProxy.sol";
import "./NativeMetaTransaction.sol";

/**
 * @title ProxyRegistry
 * @author Wyvern Protocol Developers
 */
contract ProxyRegistry is ContextMixin, OwnableUpgradeable, NativeMetaTransaction, ReentrancyGuardUpgradeable {

    /* DelegateProxy implementation contract. Must be initialized. */
    address public delegateProxyImplementation;

    /* Authenticated proxies by user. */
    mapping(address => OwnableDelegateProxy) public proxies;

    /* Contracts pending access. */
    mapping(address => uint) public pending;

    /* Contracts allowed to call those proxies. */
    mapping(address => bool) public contracts;

    /* Delay period for adding an authenticated contract.
       This mitigates a particular class of potential attack on the Wyvern DAO (which owns this registry) - if at any point the value of assets held by proxy contracts exceeded the value of half the WYV supply (votes in the DAO),
       a malicious but rational attacker could buy half the Wyvern and grant themselves access to all the proxy contracts. A delay period renders this attack nonthreatening - given two weeks, if that happened, users would have
       plenty of time to notice and transfer their assets.
    */
    uint public delayPeriod;

    function __ProxyRegistry_init() internal initializer {
        __Ownable_init();
        __ReentrancyGuard_init();
        _initializeEIP712();
        delayPeriod = 2 days;
    }
    
    /**
     * Sets the delay period. Can be zero if decided by owner.
     *
     * @dev ProxyRegistry owner only
     * @param _delayPeriod new delay period in seconds
     */
    function setDelayPeriod (uint256 _delayPeriod)
        external
        onlyOwner
    {
        delayPeriod = _delayPeriod;
    }

    /**
     * Start the process to enable access for specified contract. Subject to delay period.
     *
     * @dev ProxyRegistry owner only
     * @param addr Address to which to grant permissions
     */
    function startGrantAuthentication (address addr)
        external
        onlyOwner
    {
        require(addr != address(0), "Input address cannot be zero address");
        require(!contracts[addr] && pending[addr] == 0, "Contract is already allowed in registry, or pending");
        pending[addr] = block.timestamp;
    }

    /**
     * End the process to enable access for specified contract after delay period has passed.
     *
     * @dev ProxyRegistry owner only
     * @param addr Address to which to grant permissions
     */
    function endGrantAuthentication (address addr)
        external
        onlyOwner
    {
        require(!contracts[addr] && pending[addr] != 0 && ((pending[addr] + delayPeriod) < block.timestamp), "Contract is no longer pending or has already been approved by registry");
        pending[addr] = 0;
        contracts[addr] = true;
    }

    /**
     * End the process to decline access for specified contract.
     *
     * @dev ProxyRegistry owner only
     * @param addr Address to which to decline permissions
     */
    function declineGrantAuthentication (address addr)
        external
        onlyOwner
    {
        require(!contracts[addr] && pending[addr] != 0, "Contract is no longer pending or has already been approved by registry");
        pending[addr] = 0;
        contracts[addr] = false;
    }

    /**
     * Revoke access for specified contract. Can be done instantly.
     *
     * @dev ProxyRegistry owner only
     * @param addr Address of which to revoke permissions
     */    
    function revokeAuthentication (address addr)
        external
        onlyOwner
    {
        contracts[addr] = false;
    }

    /**
     * Register a proxy contract with this registry
     *
     * @dev Must be called by the user which the proxy is for, creates a new AuthenticatedProxy
     * @return proxy New AuthenticatedProxy contract
     */
    function registerProxy()
        external
        returns (OwnableDelegateProxy proxy)
    {
        return registerProxyFor(_msgSender());
    }

    /**
     * Register a proxy contract with this registry, overriding any existing proxy
     *
     * @dev Must be called by the user which the proxy is for, creates a new AuthenticatedProxy
     * @return proxy New AuthenticatedProxy contract
     */
    function registerProxyOverride()
        external nonReentrant
        returns (OwnableDelegateProxy proxy)
    {
        proxy = new OwnableDelegateProxy(_msgSender(), delegateProxyImplementation, abi.encodeWithSignature("initialize(address,address)", _msgSender(), address(this)));
        proxies[_msgSender()] = proxy;
    }

    /**
     * Register a proxy contract with this registry
     *
     * @dev Can be called by any user
     * @return proxy New AuthenticatedProxy contract
     */
    function registerProxyFor(address user)
        public
        returns (OwnableDelegateProxy proxy)
    {
        require(proxies[user] == OwnableDelegateProxy(payable(0)), "User already has a proxy");
        proxy = new OwnableDelegateProxy(user, delegateProxyImplementation, abi.encodeWithSignature("initialize(address,address)", user, address(this)));
        proxies[user] = proxy;
    }

    /**
     * Transfer access
     */
    function transferAccessTo(address from, address to)
        external
    {
        OwnableDelegateProxy proxy = proxies[from];

        /* CHECKS */
        require(OwnableDelegateProxy(payable(_msgSender())) == proxy, "Proxy transfer can only be called by the proxy");
        require(proxies[to] == OwnableDelegateProxy(payable(0)), "Proxy transfer has existing proxy as destination");

        /* EFFECTS */
        delete proxies[from];
        proxies[to] = proxy;
    }

    function _msgSender() internal view override returns (address sender) {
        return ContextMixin.msgSender();
    }
}
